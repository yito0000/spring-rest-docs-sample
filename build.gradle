buildscript {
	ext {
		springBootVersion = '2.0.4.RELEASE'
	}
	repositories {
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("org.junit.platform:junit-platform-gradle-plugin:1.1.0")
		classpath("org.asciidoctor:asciidoctor-gradle-plugin:1.5.3")
		classpath("com.epages:restdocs-raml-gradle-plugin:0.4.1")
	}
}
plugins {
	id "org.asciidoctor.convert" version "1.5.3"
	id "io.spring.dependency-management" version "1.0.5.RELEASE"
}

subprojects {
	repositories {
		mavenCentral()
		jcenter()
	}

	apply plugin: 'java'
	apply plugin: 'eclipse'
	apply plugin: 'idea'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.junit.platform.gradle.plugin'

	group = 'com.sample.docs.spring'
	version = '0.0.1-SNAPSHOT'
	sourceCompatibility = 1.8
	targetCompatibility = 1.8

	dependencies {
		compile('org.springframework.boot:spring-boot-starter')
		compile('org.springframework.boot:spring-boot-starter-web')
		compile('org.springframework.boot:spring-boot-starter-hateoas')

		testCompile('org.springframework.boot:spring-boot-starter-test')
		testCompile("org.springframework.restdocs:spring-restdocs-mockmvc")

		testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')
		testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
	}

}

project(':rest-docs') {
	apply plugin: 'org.asciidoctor.convert'

	repositories {
		mavenCentral()
	}

	dependencies {
		asciidoctor('org.springframework.restdocs:spring-restdocs-asciidoctor')
	}

	ext {
		snippetsDir = file('build/generated-snippets')
	}

	test {
		outputs.dir snippetsDir
	}

	asciidoctor {
		inputs.dir snippetsDir
		dependsOn test
	}

	bootJar {
		dependsOn asciidoctor
		from ("${asciidoctor.outputDir}/html5") {
			into 'static/docs'
		}
	}
}

project('rest-docs-raml') {
	apply plugin: 'com.epages.restdocs-raml'

	repositories {
		mavenCentral()
		jcenter()
		maven {
			url 'https://jitpack.io'
		}
	}

	dependencies {
		testCompile('com.epages:restdocs-raml:0.4.1')
		testCompile('org.json:json:20170516')
	}

	ramldoc {
		apiTitle = 'Notes API'
		apiBaseUri = 'http://localhost:8080/'
		ramlVersion = "1.0"
	}
}
